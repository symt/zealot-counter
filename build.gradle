buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

apply plugin: 'net.minecraftforge.gradle.forge'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
version = "1.4.0"
group= "io.github.symt"
archivesBaseName = "ZealotCounter"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_20"
    makeObfSourceJar = false
}

repositories {
    jcenter()
}


dependencies {
    compile group: 'org.json', name: 'json', version: '20190722'

    provided files("libs/modcore.jar")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes "FMLCorePlugin": "io.github.symt.ZealotCounter.modcore.FMLLoadingPlugin",
                "ModSide": "CLIENT",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does"
    }
}

shadowJar {
    classifier = ""

    relocate "org.json", "io.github.symt.repack.org.json"
}

reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar